?value: _assignment | file

EQKEY.2 : /[a-zA-Z]+\(\S+\)[a-zA-Z]*/

MACRO_TERM : /[\-]?\$[^;\s\n\)]+/
DOUBLE_NAME.1 : /[a-zA-Z][a-zA-Z\d]*[:\.-][a-zA-Z\d\._\::]+/

_list_name: CNAME|ESCAPED_STRING|DOUBLE_NAME|macro|SIGNED_NUMBER
_dict_name: CNAME|ESCAPED_STRING|DOUBLE_NAME|EQKEY
_var_name : CNAME|ESCAPED_STRING|DOUBLE_NAME|macro|EQKEY

LIST_TYPE.2 : /List<[\w]+>/
list_info : _list_name [CNAME] [ NEWLINE LIST_TYPE ] [NEWLINE SIGNED_NUMBER]

directive: /#[a-zA-Z]+/ | /#[a-zA-Z]+{[^}]*}/
macro : MACRO_TERM
dimension_set : "[" (SIGNED_NUMBER)+ "]"
_dataentry : DOUBLE_NAME | CNAME | list | ESCAPED_STRING | macro | directive
    | SIGNED_NUMBER | dimension_set | EQKEY

list : "(" (_dataentry|NEWLINE|dict_assignment)* ")"

directive_assignment: [NEWLINE] directive (CNAME|EQKEY|ESCAPED_STRING) [";"] NEWLINE
macro_assignment : [NEWLINE] macro ";" NEWLINE
list_assignment : [NEWLINE] list_info NEWLINE list ";" NEWLINE
var_assignment : [NEWLINE] _var_name [(_dataentry)+] ";" NEWLINE
dict_assignment : [NEWLINE] _dict_name NEWLINE "{" [NEWLINE] [(_assignment)+] "}" [";"] NEWLINE
code_assignment : [NEWLINE] CNAME NEWLINE /#{[\S\s]*?#};/ NEWLINE
dimension_assignment : [NEWLINE] CNAME [CNAME] dimension_set _dataentry ";" NEWLINE
_assignment : var_assignment | dict_assignment | list_assignment
              | dimension_assignment | code_assignment | macro_assignment | directive_assignment

file : (_assignment)+

CPP_COMMENT: /\/\/[^\n]*/ NEWLINE
C_COMMENT: "/*" /(.|\n)*?/ "*/" NEWLINE

%ignore /[\n\f\r]+/
%ignore CPP_COMMENT
%ignore C_COMMENT
%import common.SIGNED_NUMBER
%import common.ESCAPED_STRING
%import common.WS
%ignore WS
%import common.NEWLINE
%import common.CNAME
